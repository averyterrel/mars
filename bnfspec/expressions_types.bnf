<fn> ::= <fn_type> <stmt_block>

<fn_type> ::= "fn" "(" (<param_list> ("," | E) | E) ")" ("->" (<type> | "(" <param_list> ")") | E)

<param> ::= <identifier> ":" <type> 
<multi_param> ::= (<identifier>  ",")+ <identifier>  ":" <type> 
<param_list> ::= <param_list> "," <param> | 
                 <param_list> "," <multi_param> | 
                 <param> | <multi_param>

<type> ::= "i8" | "i16" | "i32" | "i64" | "int" |
           "u8" | "u16" | "u32" | "u64" | "uint" |
           "f16" | "f32" | "f64" | "float" | "bool" | <identifier> | ("^" | "[]") ("mut" | "let") ( <type> | E) |
           "["  (<digit>+ | E)  "]" <type> | <fn_type> | <aggregate>

<expression> ::= <expression> <binop> <expression> | 
                 <unop> <expression> | <fn> |
                 ("cast" | "bitcast") "(" <type> ")" <expression> | <atomic_expression>

<atomic_expression> ::= <atomic_expression> ("::" | "." | "->") <identifier> |
                        <atomic_expression> "[" <expression> "]" |
                        <atomic_expression> "[" (<expression> | E) ":"  (<expression> | E) "]" |
                        <atomic_expression> "(" ((<expression> ",")* <expression> ("," | E) | E) ")" |
                        <literal> | <identifier> | "(" <expression> ")" |
                        <atomic_expression> "^" |
                        "." <identifier>

<binop> ::= "*" | "/" | "%" | "%%" | "&" | "<<" | ">>" | "~|" |
               "+" | "-" | "|" | "~" |
               "==" | "!=" | "<" | ">" | "<=" | ">=" |
               "&&" | "||" 

<unop> ::= "&" | "-" | "~" | "!"

<aggregate> ::= ("struct" | "union")  "{" 
                ( <identifier> ":" <type> ",")+ 
                "}" ";" 
<enum> ::= "enum" <type> "{" 
           (<identifier> ",")+ "}" ";" 